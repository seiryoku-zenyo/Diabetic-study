//By Vasco Fachada, September 2019
//Study lpid droplets and plin5 in skeletal muscle of diabeic, obese and lean humans
//This macro fetches the image files, preforms deconvolution and runs the cell analyser plugin

dir = getDirectory("Select a directory containing your groups");
start = getTime();
grp_folds = getFileList(dir);

setBatchMode(true);

for(f=0; f<grp_folds.length; f++) {
	cur_grp = getFileList(dir+grp_folds[f]);
	for(h = 0; h < cur_grp.length; h++) {
		subj_fold = getFileList(dir+grp_folds[f]+cur_grp[h]);
		
		for(g=0; g<subj_fold.length; g++){
			if(matches(subj_fold[g], "LDs.tif") || matches(subj_fold[g], "OXPAT.tif")){
				cur_fold = dir+grp_folds[f]+cur_grp[h];
				cur_subj = cur_grp[h];
				img_name = subj_fold[g];
				datafile = cur_fold+img_name;
				print(cur_fold);
				print(grp_folds[f]);
				print(cur_subj);
				print(img_name);
				img_psf = "PSF_"+img_name;
				open(File.separator+"/fileservices.ad.jyu.fi\\homes\\varufach\\Desktop\\research\\Diabetic_study_for_TOPOCELL\\code\\PSF_"+img_name);
				rename(img_psf);
				open(datafile);
				deconvolve(datafile, img_name, img_psf, cur_fold);
				while (nImages>0) { 
					selectImage(nImages); 
        			close();    				  
				} 
			}
		}
	}
}

end = getTime();
Dialog.create("Job Done!");
Dialog.addMessage("Finished within "+(end-start)/1000+" seconds ("+(end-start)/1000/60+" minutes).");
Dialog.show();

function deconvolve(datafile, img_name, img_psf, cur_fold){
// Job: Job
// Macro generated by DeconvolutionLab2
// 18/09/19 14:29:6
image = " -image platform "+img_name;
psf = " -psf platform "+img_psf;
algorithm = " -algorithm RL 10";
parameters = "";
parameters += " -path "+cur_fold;
run("DeconvolutionLab2 Run", image + psf + algorithm + parameters);
while (!isOpen("Final Display of RL")){
	wait(3000);
	//just wait for plugin to finish
}
selectWindow("Final Display of RL");
run("8-bit");
run("HiLo");
saveAs("Tiff", cur_fold+File.separator+"deconv_"+img_name);
}